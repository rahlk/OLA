plugins {
    id 'java'
    id 'application'
    id 'maven-publish'
    id 'org.ajoberstar.grgit' version '4.1.0'
}

repositories {
    mavenLocal()
    maven {
        url = uri('https://repo.maven.apache.org/maven2/')
    }

    maven {
        url = uri('https://maven.pkg.github.com/CodeShield-Security/SPDS/')
    }
}

dependencies {
    implementation 'com.ibm.wala:com.ibm.wala.shrike:1.5.6'
    implementation 'com.ibm.wala:com.ibm.wala.util:1.5.6'
    implementation 'com.ibm.wala:com.ibm.wala.core:1.5.6'
    implementation 'com.ibm.wala:com.ibm.wala.cast:1.5.6'
    implementation 'com.ibm.wala:com.ibm.wala.cast.js:1.5.6'
    implementation 'com.ibm.wala:com.ibm.wala.cast.js.rhino:1.5.6'
    implementation 'com.ibm.wala:com.ibm.wala.cast.java:1.5.6'
    implementation 'com.ibm.wala:com.ibm.wala.cast.java.ecj:1.5.6'
    implementation 'com.ibm.wala:com.ibm.wala.dalvik:1.5.6'
    implementation 'com.ibm.wala:com.ibm.wala.scandroid:1.5.6'
    implementation 'org.slf4j:slf4j-api:2.0.0-alpha1'
    implementation 'org.slf4j:slf4j-log4j12:2.0.0-alpha1'
    implementation 'org.soot-oss:soot:4.2.1'
    implementation 'in.ac.iitb.cse:vasco:1.1-SNAPSHOT'
    implementation 'de.fraunhofer.iem:WPDS:3.1.2-SNAPSHOT'
    implementation 'de.fraunhofer.iem:boomerangPDS:3.1.2-SNAPSHOT'
    testImplementation 'junit:junit:4.13.1'
}

group = 'com.ibm'
version = '1'
description = 'ola'
java.sourceCompatibility = JavaVersion.VERSION_1_8

publishing {
    publications {
        maven(MavenPublication) {
            from(components.java)
        }
    }
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

application {
    mainClassName = project.findProperty("mainClass").toString()
}

task tagRelease {
  description = 'Tags the current head with the project\'s version.'
  doLast {
    grgit.tag.add {
      name = version
      message = "Release of ${version}"
    }
  }
}

task pushToOrigin {
  description = 'Pushes current branch\'s committed changes to origin repo.'
  doLast {
    // The next two lines are equivalent to git add --all . 
    grgit.add(patterns: ["."], update: true)
    grgit.add(patterns: ["."])
    // Git commit w/wo a commit message
    if (project.hasProperty("message") )
        grgit.commit(message: message, all: true)
    else
        grgit.commit(message: "Autocommit from gradle", all: true)
    grgit.push()
  }
}